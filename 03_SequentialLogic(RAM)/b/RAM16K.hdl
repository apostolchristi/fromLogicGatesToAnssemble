// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM16K.hdl

/**
 * Memory of 16K registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM16K {
    IN in[16], load, address[14];
    OUT out[16];

	PARTS:
    	/**Think about RAM's address input as consistingof two fields MSB & LSB;
		a. One field can be used to select a RAM-part (MSB - Most Significant Bits);
		b. The otherfield can be used to select a register withtin that RAM-part (LSB - Less Significant Bits)
	
															   MSB    LSB
												  address[14] = 00|000000000000
	*/
	
	DMux4Way(in=load, sel=address[12..13], a=loadRAMpart1, b=loadRAMpart2, c=loadRAMpart3, d=loadRAMpart4);
	
    RAM4K(in=in, load=loadRAMpart1, address=address[0..11], out=ram1out);
	RAM4K(in=in, load=loadRAMpart2, address=address[0..11], out=ram2out);
	RAM4K(in=in, load=loadRAMpart3, address=address[0..11], out=ram3out);
	RAM4K(in=in, load=loadRAMpart4, address=address[0..11], out=ram4out);
	
	Mux4Way16(a=ram1out, b=ram2out, c=ram3out, d=ram4out, sel=address[12..13], out=out);
}